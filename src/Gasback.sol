// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.7;

/// @dev A contract that converts a portion of the gas burned into ETH.
/// This contract holds ETH deposited by the sequencer, which will be
/// redistributed to callers.
/// @dev Configuration notes:
/// - The baseFeeVault's WITHDRAWAL_NETWORK should be configured to withdraw to the network this contract is deployed on.
/// - The baseFeeVault's MIN_WITHDRAWAL_AMOUNT should be set to a reasonable value below ethToGive in order for withdrawals to be successful.
contract Gasback {
    /*«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-*/
    /*                         CONSTANTS                          */
    /*-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»*/

    /// @dev The address authorized to configure the contract.
    address internal constant _SYSTEM_ADDRESS = 0xffffFFFfFFffffffffffffffFfFFFfffFFFfFFfE;

    /// @dev The denominator of the gasback ratio.
    uint256 public constant GASBACK_RATIO_DENOMINATOR = 1 ether;

    /*«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-*/
    /*                          STORAGE                           */
    /*-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»*/

    /// @dev Storage struct for the gasback contract.
    struct GasbackStorage {
        // The gasback ratio numerator.
        uint256 gasbackRatioNumerator;
        // If the base fee exceeds this, this contract becomes a pass through.
        uint256 gasbackMaxBaseFee;
        // The base fee vault predeploy on OP stack chains.
        // If this contract used as an EIP-7702 delegated EOA which is also the
        // recipient of the base fee vault, it can be configured to auto-pull
        // funds from the base fee vault when it runs out of ETH.
        address baseFeeVault;
    }

    /// @dev Returns a pointer to the storage struct.
    function _getGasbackStorage() internal pure returns (GasbackStorage storage $) {
        // Truncate to 9 bytes to reduce bytecode size.
        uint256 s = uint72(bytes9(keccak256("GASBACK_STORAGE")));
        /// @solidity memory-safe-assembly
        assembly {
            $.slot := s
        }
    }

    /*«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-*/
    /*                        CONSTRUCTOR                         */
    /*-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»*/

    /// @dev When using this contract with EIP-7702, setters must be called to configure the constructor values.
    constructor() payable {
        GasbackStorage storage $ = _getGasbackStorage();
        $.gasbackRatioNumerator = 0.6 ether;
        $.gasbackMaxBaseFee = type(uint256).max;
        $.baseFeeVault = 0x4200000000000000000000000000000000000019;
    }

    /*«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-*/
    /*                       VIEW FUNCTIONS                       */
    /*-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»*/

    /// @dev The gasback ratio numerator.
    function gasbackRatioNumerator() public view virtual returns (uint256) {
        return _getGasbackStorage().gasbackRatioNumerator;
    }

    /// @dev If the base fee exceeds this, this contract becomes a pass through.
    function gasbackMaxBaseFee() public view virtual returns (uint256) {
        return _getGasbackStorage().gasbackMaxBaseFee;
    }

    /// @dev The base fee vault on OP stack chains.
    function baseFeeVault() public view virtual returns (address) {
        return _getGasbackStorage().baseFeeVault;
    }

    /*«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-*/
    /*                      ADMIN FUNCTIONS                       */
    /*-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»*/

    /// @dev Withdraws ETH from this contract.
    function withdraw(address to, uint256 amount) public onlySystemOrThis returns (bool) {
        /// @solidity memory-safe-assembly
        assembly {
            if iszero(call(gas(), to, amount, 0x00, 0x00, 0x00, 0x00)) { revert(0x00, 0x00) }
        }
        return true;
    }

    /// @dev Sets the numerator for the gasback ratio.
    function setGasbackRatioNumerator(uint256 value) public onlySystemOrThis returns (bool) {
        require(value <= GASBACK_RATIO_DENOMINATOR);
        _getGasbackStorage().gasbackRatioNumerator = value;
        return true;
    }

    /// @dev Sets the max base fee.
    function setGasbackMaxBaseFee(uint256 value) public onlySystemOrThis returns (bool) {
        _getGasbackStorage().gasbackMaxBaseFee = value;
        return true;
    }

    /// @dev Sets the base fee vault.
    function setBaseFeeVault(address value) public onlySystemOrThis returns (bool) {
        _getGasbackStorage().baseFeeVault = value;
        return true;
    }

    /// @dev A noop function.
    function noop() public payable returns (bool) {
        return true;
    }

    /// @dev Pulls from the base fee vault and reverts unless this contract has enough ETH after.
    function triggerBaseFeeVaultWithdraw(uint256 expectedSelfBalanceAfter) external onlySelf {
        (bool success,) =
            _getGasbackStorage().baseFeeVault.call(abi.encodeWithSignature("withdraw()"));
        require(success);
        require(address(this).balance >= expectedSelfBalanceAfter);
    }

    /// @dev Guards the function such that it can only be called either by
    /// the system contract, or by the contract itself (as an EIP-7702 delegated EOA).
    modifier onlySystemOrThis() {
        require(msg.sender == _SYSTEM_ADDRESS || msg.sender == address(this));
        _;
    }

    /// @dev Guards the function such that it can only be called by the contract itself.
    modifier onlySelf() {
        require(msg.sender == address(this));
        _;
    }

    /*«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-«-*/
    /*                          GASBACK                           */
    /*-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»-»*/

    /// @dev For the gasback logic.
    fallback() external payable {
        require(tx.gasprice != 0, "Gasback: gasprice is 0"); // Prevents L1 -> L2 deposits from being used.
        uint256 gasToBurn;

        /// @solidity memory-safe-assembly
        assembly {
            gasToBurn := calldataload(0x00)
            // The input must be exactly 32 bytes.
            if iszero(eq(calldatasize(), 0x20)) {
                // Use `invalid` to burn all the gas passed in efficiently via the self-call.
                if eq(caller(), address()) { invalid() }
                revert(0x00, 0x00)
            }
        }

        GasbackStorage storage $ = _getGasbackStorage();

        uint256 ethFromGas = gasToBurn * block.basefee;
        uint256 ethToGive = (ethFromGas * $.gasbackRatioNumerator) / GASBACK_RATIO_DENOMINATOR;

        uint256 selfBalance = address(this).balance;
        // If the contract has insufficient ETH, try to pull from the base fee vault.
        if (ethToGive > selfBalance && block.basefee <= $.gasbackMaxBaseFee) {
            /// @solidity memory-safe-assembly
            assembly {
                mstore(0x00, 0xc70746b1) // `triggerBaseFeeVaultWithdraw(uint256)`. we don't check success here because it will revert if the base fee vault is out of ETH.
                mstore(0x20, ethToGive)
                pop(call(gas(), address(), 0, 0x1c, 0x24, 0x00, 0x00))
            }
        }

        // If the contract has insufficient ETH, or if the base fee is too high.
        if (ethToGive > address(this).balance || block.basefee > $.gasbackMaxBaseFee) {
            // Do a pass through.
            ethToGive = 0;
            gasToBurn = 0;
        }
        /// @solidity memory-safe-assembly
        assembly {
            if gasToBurn {
                let gasBefore := gas()
                // Make a self-call to burn `gasToBurn`.
                pop(staticcall(gasToBurn, address(), 0x00, 0x01, 0x00, 0x00))
                // Require that the amount of gas burned is greater or equal to `gasToBurn`.
                if lt(sub(gasBefore, gas()), gasToBurn) { revert(0x00, 0x00) }
            }

            if ethToGive {
                // First, attempt to send the ETH to the caller via a call.
                if iszero(call(gas(), caller(), ethToGive, 0x00, 0x00, 0x00, 0x00)) {
                    // And if it fails, force send the ETH via a `SELFDESTRUCT` contract.
                    mstore(0x00, caller()) // Store the address in scratch space.
                    mstore8(0x0b, 0x73) // Opcode `PUSH20`.
                    mstore8(0x20, 0xff) // Opcode `SELFDESTRUCT`.
                    if iszero(create(ethToGive, 0x0b, 0x16)) { revert(0x00, 0x00) }
                }
            }

            mstore(0x00, ethToGive)
            return(0x00, 0x20) // Return the `ethToGive`.
        }
    }

    /// @dev For depositing ETH.
    receive() external payable {}
}
